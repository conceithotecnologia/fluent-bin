/**
   Checa se o CNPJ informado é valido

   Autor	   Ricardo Gonçalves
   Data     21/11/2008 20:54:25
   
   Retorno  0 - Código Ok
            1 - Composição inválida
            2 - CNPJ inválido

	Histórico
	------------------------------------------------------------------
      21/11/2008 20:56:25  v1.0  Ricardo Gonçalves.
         [+] Versão inicial.
*/
Create or Replace Function sys_cnpj_check
(  in cnpj varchar(14))
Returns integer
as $$
Declare
-- Variaveis
   i        INT4;           -- Contador
   iProd    INT4;           -- Somatório
   iMult    INT4;           -- Fator
   iDigito  INT4;           -- Digito verificador calculado
   vCnpj    varchar(14);
Begin
   if not cnpj ~ '[0-9]{14}' then
      return 1;
   end if;

   -- *****************************************************
   -- Verifica a validade do CNPJ
   -- *****************************************************

   -- primeiro digito
   vCnpj := substring(cnpj, 1, 12);
   iMult := 2;
   iProd := 0;

   for i in reverse 12..1 loop
      iProd := iProd + substring(vCnpj, i, 1)::int2 * iMult;

      if iMult = 9 then
         iMult := 2;
      else
         iMult := iMult + 1;
      end if;
   end loop;

   iDigito := 11 - (iProd % 11);
   if iDigito >= 10 then
      iDigito := 0;
   end if;

   vCnpj := substring(cnpj, 1, 12) || iDigito::varchar(1);

   -- segundo digito
   iMult := 2;
   iProd := 0;

   FOR i IN REVERSE 13..1 LOOP
      iProd := iProd + substring(vcnpj, i, 1)::int2 * iMult;
      IF iMult = 9 THEN
         iMult := 2;
      ELSE
         iMult := iMult + 1;
      END IF;
   END LOOP;

   iDigito := 11 - (iProd % 11);
   IF iDigito >= 10 THEN
      iDigito := 0;
   END IF;

   vCnpj := vCnpj || iDigito::varchar(1);

   if vCnpj <> cnpj then
      return 2;
   end if;

   return 0;
End;
$$ language 'plpgsql';